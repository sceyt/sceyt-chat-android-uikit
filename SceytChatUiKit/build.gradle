plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

apply plugin: 'maven-publish'

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

def getVersionName = { ->
    return "1.5.36"
}

def getArtifactId = { ->
    return "sceytkit"
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'com.sceytkit'
            artifactId getArtifactId()
            version getVersionName()

            artifact sourceJar
            artifact("$buildDir/outputs/aar/SceytChatUiKit-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/maratsct/TestSceyt")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}

android {
    compileSdk compile_sdk_version

    defaultConfig {
        minSdk min_sdk_version
        targetSdk target_sdk_version

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
}


dependencies {
    implementation project(':SceytSDK-debug')
    implementation project(':AXEmojiView')

    api supportDependencies.coreKtx
    api supportDependencies.appCompat
    api supportDependencies.material
    api supportDependencies.constraintlayout
    api supportDependencies.recyclerView
    api supportDependencies.fragmentKtx
    api supportDependencies.lifecycleRuntime
    api supportDependencies.liveEvent
    api supportDependencies.glide
    api supportDependencies.glideTransformations
    implementation supportDependencies.axEmoji
    api supportDependencies.ion
    api supportDependencies.firebaseBom
    api supportDependencies.firebaseMessaging
    api supportDependencies.okHttp
    api supportDependencies.voley
    kapt supportDependencies.roomCompiler
    api supportDependencies.roomRuntime
    api supportDependencies.roomKtx
    api supportDependencies.koinAndroid
    api supportDependencies.exoPlayerMedia
    api supportDependencies.exoPlayerUi
    api supportDependencies.lottie
    api supportDependencies.asyncLayoutInflater
    api supportDependencies.moshiConverter
    api supportDependencies.jsoup
    api supportDependencies.flexbox
    api supportDependencies.sdp
    api supportDependencies.ssp
    api supportDependencies.lightCompressor
    api supportDependencies.workRuntimeKtx
}

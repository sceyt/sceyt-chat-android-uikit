plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

apply plugin: 'maven-publish'

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

def getVersionName = { ->
    return "1.3.2"
}

def getArtifactId = { ->
    return "sceytkit"
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'com.sceytkit'
            artifactId getArtifactId()
            version getVersionName()

            artifact sourceJar
            artifact("$buildDir/outputs/aar/SceytChatUiKit-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/maratsct/TestSceyt")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
}


dependencies {
    implementation project(':SceytSDK-debug')
    implementation project(':AXEmojiView')

    implementation supportDependencies.coreKtx
    implementation supportDependencies.appCompat
    implementation supportDependencies.material
    implementation supportDependencies.constraintlayout
    implementation supportDependencies.recyclerView
    implementation supportDependencies.fragmentKtx
    implementation supportDependencies.lifecycleRuntime
    implementation supportDependencies.glide
    implementation supportDependencies.axEmoji
    implementation supportDependencies.ion
    implementation supportDependencies.firebaseBom
    implementation supportDependencies.firebaseMessaging
    implementation supportDependencies.okHttp
    implementation supportDependencies.voley
    kapt supportDependencies.roomCompiler
    implementation supportDependencies.roomRuntime
    api supportDependencies.roomKtx
    api supportDependencies.koinAndroid
    api supportDependencies.exoPlayerMedia
    api supportDependencies.exoPlayerUi
    api supportDependencies.lottie
    api supportDependencies.asyncLayoutInflater
    implementation supportDependencies.moshiConverter
    implementation supportDependencies.jsoup
    implementation supportDependencies.flexbox
    implementation supportDependencies.sdp
    implementation supportDependencies.ssp
}

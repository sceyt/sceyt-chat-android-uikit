apply plugin: 'maven-publish'
apply plugin: 'signing'

tasks.register('androidSourcesJar', Jar) {
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.register('androidJavadocs', Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    excludes = ['**/*.kt']
}

tasks.register('androidJavadocsJar', Jar) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId maven_central_publish_group
                artifactId maven_central_atifact_id
                version maven_central_version

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact androidJavadocsJar

                pom {
                    name = maven_central_atifact_id
                    description = 'Sceyt Chat Android UIKit'
                    url = 'https://github.com/sceyt/sceyt-chat-android-uikit'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/sceyt/sceyt-chat-android-uikit/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'maratsct'
                            name = 'Marat Hambikyan'
                            email = 'marat@sceyt.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/sceyt/sceyt-chat-android-uikit.git'
                        developerConnection = 'scm:git:ssh://github.com/sceyt/sceyt-chat-android-uikit.git'
                        url = 'https://github.com/sceyt/sceyt-chat-android-uikit/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}